class Repository
types
values
-- TODO Define values here

instance variables
	public name: Utils`String;
	public description: Utils`String := [];
	public isPrivate: bool;
	
	public owner: Account;
	public collaborators: set of User := {};
	public branches: map Utils`String to Branch := { |-> };
	public defaultBranch: Branch;

	inv defaultBranch in set rng branches;
	inv branches(defaultBranch.name) = defaultBranch;

operations
	public Repository: Utils`String * Account * bool ==> Repository
	Repository(n, acc, priv) == (
		name := n;
		isPrivate := priv;
		owner := acc;
		if isofclass(User, owner) then collaborators := {owner};
		return self;
	)
	pre n <> []
	post name = n and isPrivate = priv and owner = acc and
		(isofclass(Organization, owner) or (isofclass(User, owner) and owner in set collaborators));

	public setDescription: Account * Utils`String ==> ()
	setDescription(acc, desc) == description := desc
	pre acc = owner
	post description = desc;
	
	public addCollaborator: User ==> ()
	addCollaborator(usr) == collaborators := collaborators union {usr}
	post usr in set collaborators;
	
	public createBranch: Utils`String * bool ==> Branch
	createBranch(n, prot) == (
		let b = new Branch(n, prot) in (
			branches(n) := b;
			return b;
		);
	)
	pre n not in set dom branches
	post let b = branches(n) in b.name = n and b.isProtected = prot;

functions
-- TODO Define functiones here
traces
-- TODO Define Combinatorial Test Traces here
end Repository