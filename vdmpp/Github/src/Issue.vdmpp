class Issue

types
	-- Map of message ID to Message
	public Messages = map VDMUtils`String to Message;

instance variables
	private id: VDMUtils`String;
	private isClosed: bool := false;
	public title: VDMUtils`String;
	public description: VDMUtils`String;
	
	public messages: Messages := { |-> };
	
	public assignees: set of User := {};

operations
	public Issue: VDMUtils`String * VDMUtils`String * VDMUtils`String ==> Issue
	Issue(issueID, issueTitle, issueDesc) == (
		id := issueID;
		title := issueTitle;
		description := issueDesc;
		return self;
	)
	post card dom messages = 0;
	
	--Add a message to this issue
	public addMessage: Message ==> ()
	addMessage(msg) == (
		messages(msg.id) := msg;
	)
	pre not msg.id in set dom messages
	post messages(msg.id) = msg;
	
	--Assign a User to this issue
	public assignUser: User ==> ()
	assignUser(user) == (
		assignees := assignees union {user};
	)
	pre not user in set assignees
	post user in set assignees;
	
	public close: () ==> ()
	close() == isClosed := true
	pre not isClosed
	post isClosed;
	
	public reopen: () ==> ()
	reopen() == isClosed := false
	pre isClosed
	post not isClosed;
	
	public pure numMessages: () ==> nat
	numMessages() == return card dom messages;
	
	public pure numAssignees: () ==> nat
	numAssignees() == return card assignees;
	
	public pure isIssueClosed: () ==> bool
	isIssueClosed() == return isClosed;

end Issue