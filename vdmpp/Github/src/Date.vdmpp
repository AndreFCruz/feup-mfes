class Date
types
	public DateComparable :: date: Date
	ord mk_DateComparable(d1) < mk_DateComparable(d2) ==
		d1.year < d2.year or
		d1.year = d2.year and d1.month < d2.month or
		d1.year = d2.year and d1.month = d2.month and d1.day < d2.day or
		d1.year = d2.year and d1.month = d2.month and d1.day = d2.day and d1.hour < d2.hour or
		d1.year = d2.year and d1.month = d2.month and d1.day = d2.day and d1.hour = d2.hour and d1.minute < d2.minute;

values
	public static startYear = 2000;

instance variables
	private year: nat1;
	private month: nat1;
	private day: nat1;
	private hour: nat;
	private minute: nat;
	
	/**
	 * Invariants to ensure Date consistency.
	 */
	inv year >= startYear;
	inv month >= 1 and month <= 12;
	inv day >= 1 and day <= daysOfMonth(year, month);
	inv hour >= 0 and hour < 24;
	inv minute >= 0 and minute < 60;
	
operations

	/**
	 * Constructor for a Date instance.
	 */
	public Date: nat1 * nat1 * nat1 * nat * nat ==> Date
	Date(y, mo, d, h, mi) == (
		year := y;
		month := mo;
		day := d;
		hour := h;
		minute := mi;
		return self;
	) -- No need for pre conditions -> assured by instance variables invariants
	post year = y and month = mo and day = day and hour = h and minute = mi;

functions

	/**
	 * @return whether the given year is a leap year.
	 */
	public static isLeapYear(y: nat1) res: bool == y mod 4 = 0
	pre y >= startYear;

	/**
	 * @return the number of days in the given month.
	 */
	public static daysOfMonth(y, m: nat1) res : nat == (
		cases m :
			1, 3, 5, 7, 8, 10, 12 -> 31,
			4, 6, 9, 11 -> 30,
			2 -> if isLeapYear(y) then 29 else 28
		end
	)
	pre m >= 1 and m <= 12;

end Date